#Область ПрограммныйИнтерфейс

// Добавляет ресурсы в таблицу ресурсов
//
// Параметры:
//  ТаблицаРесурсов - ТаблицаЗначений - таблица ресурсов для заполнения
//
Процедура ДобавитьРесурсы(ТаблицаРесурсов) Экспорт
	
	mcp_Метаданные.ДобавитьРесурс(
		ТаблицаРесурсов,
		"file://resource/syntax_1c.txt",
		"1csyntax",
		"Описание синтаксиса встроенного языка 1С:Предприятие в формате Markdown"
	);
	
КонецПроцедуры

// Читает ресурс по адресу
//
// Параметры:
//  Адрес - Строка - адрес ресурса
//
// Возвращаемое значение:
//  Строка - содержимое ресурса
//
Функция ПрочитатьРесурс(Адрес) Экспорт
	
	Если СтрНачинаетсяС(Адрес, "file://resource/") Тогда
		
		ПозицияНачалаИмени = СтрДлина("file://resource/") + 1;
		ПозицияНачалаРасширения = СтрНайти(Адрес, ".");
		ИмяМакета = Сред(Адрес, ПозицияНачалаИмени, ПозицияНачалаРасширения - ПозицияНачалаИмени);
		МакетСинтаксиса = Обработки.mcp_РесурсОписаниеСинтаксисаВстроенногоЯзыка.ПолучитьМакет(ИмяМакета);
		Возврат МакетСинтаксиса.ПолучитьТекст();
		
	КонецЕсли;
	
	ВызватьИсключение СтрШаблон("Неизвестный адрес ресурса: %1", Адрес);
	
КонецФункции

#КонецОбласти
